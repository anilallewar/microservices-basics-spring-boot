/*
* Build file for Comments-webservice
*/
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'eclipse'
apply plugin: 'spring-cloud-contract'
apply plugin: 'maven-publish'

buildscript {
	project.ext {
         springBootVersion = '1.5.6.RELEASE'
         jarName = 'basic-comments-webservice'
         versionName = '0.0.1'
         gradleDockerVersion = '1.2'
         springCloudContractVesrion = '1.1.1.RELEASE'
	}
    repositories {
        jcenter()
        mavenCentral()
    }
    
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.springBootVersion}"
        
        // Gradle docker plugin dependency
        classpath "se.transmode.gradle:gradle-docker:${project.gradleDockerVersion}"
        
        // if using Stub Runner (consumer side) only remove this dependency
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${project.springCloudContractVesrion}"
    }
}

task createWrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

// Used by the Docker gradle plugin, group refers to the account under which the docker image is created
group = 'anilallewar'
mainClassName = 'com.anilallewar.microservices.comments.CommentsApplication'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

ext {
	springCloudVersion = 'Dalston.SR3'
}

dependencyManagement {
  imports {
    mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
  }
}

dependencies {
    // Basic Spring boot with config client
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile("org.springframework.boot:spring-boot-starter-web")    
    compile("org.springframework.boot:spring-boot-starter-actuator")
    
    // Spring OAuth2 security
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.security.oauth:spring-security-oauth2")
    compile("org.springframework.security:spring-security-jwt")
    
    // Eureka client
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    
    // Consumer driven contracts server
    testCompile('org.springframework.cloud:spring-cloud-starter-contract-verifier')
	
	// Zipkin tracing
	compile('org.springframework.cloud:spring-cloud-starter-zipkin')
}

jar {
    baseName = "${project.jarName}"
    version =  "${project.versionName}"
}

/*
* This task builds the docker image by copying the output of the "jar" gradle command
* and moving it to the "build/docker" directory which is used as the staging directory 
* by the docker gradle plugin. We then build the docker image by invoking the appropriate
* Dockerfile.  
*/
task buildDocker(type: Docker, dependsOn: build) {
  push = false
  applicationName = jar.baseName
  tagVersion = jar.version
  dockerfile = file('src/main/docker/Dockerfile')
  doFirst {
    // Copy the built fat jar to the Docker plugin staging directory
    copy {
      from jar
      into stageDir
    }
    // Copy the run.sh file to the Docker plugin staging directory
    copy {
      from "${project.buildDir}/resources/main/run.sh"
      into stageDir
    }
  }
}

run {
    jvmArgs = ['-Xdebug', '-Xrunjdwp:server=y,transport=dt_socket,address=4300,suspend=n','-Dspring.profiles.active=default']
}

// Setup the package which contains base classes that the spring cloud contract test case would extend
contracts {
	packageWithBaseClasses = 'com.anilallewar.microservices.comments.contracts'
}

clean.doFirst {
	delete "${System.properties['user.home']}${File.separator}.m2${File.separator}repository${File.separator}anilallewar${File.separator}basic-comments-webservice"
	delete "${System.properties['user.home']}${File.separator}.m2${File.separator}repository${File.separator}anilallewar${File.separator}basic-comments-webservice-stubs"
}

// Setup the artifact id with which the stubs jar would be published, the group id comes from the 'group' attribute defined earlier 
publishing {
    publications {
        mavenJava(MavenPublication) {
			artifactId jar.baseName
			version jar.version
            from components.java
        }
		stubs(MavenPublication) {
			artifactId "${jar.baseName}-stubs"
			version jar.version
			artifact verifierStubsJar
		}
    }
}